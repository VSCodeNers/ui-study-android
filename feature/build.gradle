plugins {
    id 'com.android.library'
    alias(libs.plugins.jetbrains.kotlin.android)
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
//    id 'com.google.gms.google-services'
}

// 프로퍼티를 선언하고, 저장한 키값을 불러온다.
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

android {
    namespace 'com.vscodeners.ui_study_android.feature'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def firebaseGoogleClientId = properties.getProperty('firebase.google.client.id', '')
        if (!firebaseGoogleClientId.isEmpty()) {
            buildConfigField "String", 'FIREBASE_GOOGLE_CLIENT_ID', "${firebaseGoogleClientId}"
        } else {
            throw new GradleException("FIREBASE_GOOGLE_CLIENT_ID is not set in local.properties")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = jvm_target_version
    }

    dataBinding {
        enabled = true
    }
    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    // UI
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "com.google.android.material:material:$meterial_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.activity:activity-ktx:$activity_version"

    // UI - Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation libs.firebase.auth.ktx
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Coil
    implementation("io.coil-kt:coil:$coil_version")

    // 카카오 로그인 SDK
    implementation "com.kakao.sdk:v2-user:$kakao_sdk_version" // 카카오 로그인 API 모듈

    // 네이버 로그인 SDK
    implementation "com.navercorp.nid:oauth:$naver_sdk_version" // jdk 11

    // Firebase
    // Import the BoM for the Firebase platform
    implementation(platform("com.google.firebase:firebase-bom:32.3.1"))

    // Add the dependency for the Firebase Authentication library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-auth-ktx")

    // Also add the dependency for the Google Play services library and specify its version
    implementation("com.google.android.gms:play-services-auth:20.7.0")
}